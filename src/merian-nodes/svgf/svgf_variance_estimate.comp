#version 460
#extension GL_GOOGLE_include_directive    : enable

#include "svgf_graph_layout.glsl"
#include "common/normal_encode.glsl"
#include "common/colors_yuv.glsl"

/*
 * Reads the accumulated irradiance and the moments, estimates the variance,
 * and prepares the filter input `img_filter_in`.
 */

layout(set = 1, binding = 0) uniform sampler2D unused; // needed to match the filter layout
layout(set = 1, binding = 1) uniform writeonly image2D img_filter_in;

layout(push_constant, std140) uniform push_t {
    float normal_reject_rad;
    float depth_accept;
    float spatial_falloff;
    float spatial_bias;
} params;

void
main()
{
  const ivec2 ipos = ivec2(gl_GlobalInvocationID);
  if (any(greaterThanEqual(ipos, imageSize(img_filter_in)))) return;

  const GBuffer gbuf = gbuffer[gbuffer_index(ipos, imageSize(img_filter_in))];
  const vec3 normal = geo_decode_normal(gbuf.enc_normal);

  const vec4 center_irr_histlen = texelFetch(img_irr, ipos, 0);
  vec4 irr_histlen = center_irr_histlen;
  const vec2 center_moments = texelFetch(img_moments, ipos, 0).rg;
  vec2 moments = center_moments;
  float sum_w = 1.0;

  // spatial filtering when variance is 0 leads to unnecessary filtering of surfaces which variance is really 0
  // however it prevents black trails.
  const int r = 2;

  for(int yy = -r; yy <= r; yy++) {
    for(int xx = -r; xx <= r; xx++) {
      if(xx != 0 || yy != 0) { // added already
        const ivec2 p = ipos + ivec2(xx, yy);

        const GBuffer neigh_gbuf = gbuffer[gbuffer_index(p, imageSize(img_filter_in))];
        const vec3 neigh_normal = geo_decode_normal(neigh_gbuf.enc_normal);

        float w = 1.0;
        // Weight neighbors with similar normals
        w *= smoothstep(params.normal_reject_rad, 1.0, dot(normal, neigh_normal));
        // Weight neighbors with similar depth
        // w *= 1.0 - smoothstep(0.0, params.depth_reject_percent * max(gbuf.linear_z, neigh_gbuf.linear_z), abs(gbuf.linear_z - neigh_gbuf.linear_z));
        w *= exp(-10 * abs(gbuf.linear_z + dot(gbuf.grad_z, vec2(xx, yy)) - neigh_gbuf.linear_z) / params.depth_accept);
        // Weight lower with increasing history
        //w *= 1.0 - irr_histlen.a / params.spatial_threshold;

        sum_w           += w;
        irr_histlen.rgb += w * texelFetch(img_irr, p, 0).rgb;
        moments         += w * texelFetch(img_moments, p, 0).rg;
      }
    }
  }

  irr_histlen.rgb /= sum_w;
  moments         /= sum_w;

  float variance = max(0.0, moments.y - moments.x * moments.x);
  const float center_variance = max(0.0, center_moments.y - center_moments.x * center_moments.x);
  const float alpha = clamp(params.spatial_bias / pow(irr_histlen.a, params.spatial_falloff), 0, 1);
  variance = mix(center_variance, variance, alpha);
  irr_histlen.rgb = mix(center_irr_histlen.rgb, irr_histlen.rgb, alpha);


  imageStore(img_filter_in, ipos, vec4(irr_histlen.rgb, variance));
}

